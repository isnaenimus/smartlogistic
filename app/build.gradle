apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.telkom.smartlogistic"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "API_ENV"
    productFlavors {
        dev {
            dimension "API_ENV"
            versionNameSuffix "-dev"
            buildConfigField "String", "BUILD_VARIANT", '"dev"'

            buildConfigField("String", "APP_ID", '"smartlogistic-telkom"')
            buildConfigField("String", "APP_NAME", '"smartlogistic"')
            buildConfigField("String", "API_BASE_URL", '"https://api-dev.smartlogistic.id/api/"')
            buildConfigField("String", "CONTENT_TYPE", '"application/json"')
            buildConfigField("String", "AUTHORIZATION", '"Basic dGVsa29tOmRhMWMyNWQ4LTM3YzgtNDFiMS1hZmUyLTQyZGQ0ODI1YmZlYQ=="')

            applicationId "com.telkom.smartlogistic.dev"
            resValue "string", "app_name", "SmartLogistic-Dev"
            resValue "string", "package_name", "com.telkom.smartlogistic.dev"

        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "boolean", "RELEASE", "false"
        }
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release') {
            variant.getFlavors().each { flavor ->
                if (flavor.dimension == 'API_ENV' && flavor.name == 'dev') {
                    variant.setIgnore(true)
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true
        quiet true
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':framework')
    implementation project(path: ':datamodule')

    annotationProcessor "org.permissionsdispatcher:permissionsdispatcher-processor:$permission_dispatcher_version"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:$permission_dispatcher_version"
    debugImplementation "com.ashokvarma.android:gander-imdb:$gander_version"
    releaseImplementation "com.ashokvarma.android:gander-no-op:$gander_version"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'
    implementation 'com.chaos.view:pinview:1.4.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}